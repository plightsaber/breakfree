$module ()
$import Modules.GeneralTools.lslm();

integer _armBoundExternal = FALSE;
string _restraints;

list getRestraintList(string prmRestraint, string prmList) {
	list restraintList = [];
	list slots = llJson2List(prmRestraint);

	integer index = 0;
	for (index = 0; index < llGetListLength(slots); ++index) {
		string strList = llJsonGetValue(llList2String(slots, index), [prmList]);
		if (JSON_INVALID != strList) {
			list liValues = llJson2List(strList);
			restraintList += liValues;
		}
	}
	return restraintList;
}

integer isArmBound() {
	return _armBoundExternal || isSet(llJsonGetValue(_restraints, ["armBound"]));
}

integer isLegBound() {
	return isSet(llJsonGetValue(_restraints, ["legBound"]));
}

integer isGagged() {
	return isSet(llJsonGetValue(_restraints, ["gagged"]));
}

integer isOtherBound() {
	return isSet(llJsonGetValue(_restraints, ["slots", "crotch"]))
		|| isSet(llJsonGetValue(_restraints, ["slots", "hand"]));
}

integer isBound() {
	return isArmBound() || isLegBound() || isGagged() || isOtherBound();
}

integer searchRestraint(string prmType, string prmParameter, string prmValue) {
	list searchSlots = getSearchSlots(prmType);

	integer index;
	for (index = 0; index < llGetListLength(searchSlots); ++index) {
		if (llJsonGetValue(_restraints, [llList2String(searchSlots, index), prmParameter]) == prmValue) {
			return TRUE;
		}
	}

	return FALSE;
}

string getTopRestraint(string prmType) {
	if (prmType == "arm") {
		if (isSet(llJsonGetValue(_restraints, ["torso"]))) { return llJsonGetValue(_restraints, ["torso"]); }
		else if (isSet(llJsonGetValue(_restraints, ["elbow"]))) { return llJsonGetValue(_restraints, ["elbow"]); }
		else if (isSet(llJsonGetValue(_restraints, ["wrist"]))) { return llJsonGetValue(_restraints, ["wrist"]); }
	} else if (prmType == "leg") {
		if (isSet(llJsonGetValue(_restraints, ["immobilizer"]))) { return llJsonGetValue(_restraints, ["immobilizer"]); }
		else if (isSet(llJsonGetValue(_restraints, ["knee"]))) { return llJsonGetValue(_restraints, ["knee"]); }
		else if (isSet(llJsonGetValue(_restraints, ["ankle"]))) { return llJsonGetValue(_restraints, ["ankle"]); }
	} else if (prmType == "gag") {
		if (isSet(llJsonGetValue(_restraints, ["gag4"]))) { return llJsonGetValue(_restraints, ["gag4"]); }
		else if (isSet(llJsonGetValue(_restraints, ["gag3"]))) { return llJsonGetValue(_restraints, ["gag3"]); }
		else if (isSet(llJsonGetValue(_restraints, ["gag2"]))) { return llJsonGetValue(_restraints, ["gag2"]); }
		else if (isSet(llJsonGetValue(_restraints, ["gag1"]))) { return llJsonGetValue(_restraints, ["gag1"]); }
	} else if (isSet(llJsonGetValue(_restraints, [prmType]))) {
		return llJsonGetValue(_restraints, [prmType]);	// Return special slot
	}

	return JSON_NULL;
}

list getSearchSlots(string prmType) {
	if (prmType == "arm") {
		return ["wrist", "elbow", "torso"];
	} else if (prmType == "leg") {
		return ["ankle", "knee", "immobilizer"];
	} else if (prmType == "gag") {
		return ["gag1", "gag2", "gag3", "gag4"];
	}

	return [prmType];	// Assume special slot - return self
}